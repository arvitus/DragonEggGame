plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'com.gradleup.shadow' version '9.2.2'
}

version = "${project.mod_version}-${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.mod_id
}

repositories {
    mavenCentral()
    maven { url "https://maven.nucleoid.xyz/" }
    maven { url 'https://repo.bluecolored.de/releases' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // required mods
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // included mods
    include modImplementation("me.lucko:fabric-permissions-api:${project.permissions_api_version}")
    include modImplementation("eu.pb4:placeholder-api:${project.placeholder_api_version}")

    // optional mods
    compileOnly "de.bluecolored:bluemap-api:${project.bluemap_api_version}"

    // other dependencies
    shadow implementation("org.spongepowered:configurate-hocon:${project.configurate_version}")
    include implementation("net.objecthunter:exp4j:${project.exp4j_version}")
}

tasks {
    remapJar {
        dependsOn(shadowJar)
        inputFile.set(shadowJar.archiveFile)
    }

    shadowJar {
        relocationPrefix = "${project.group}.${project.mod_id}.shadow"
        enableAutoRelocation = true
        configurations = [project.configurations.shadow]
        minimize()

        from("LICENSE")
    }

    processResources {
        def map = [
            "version"          : project.mod_version,
            "minecraft_version": project.minecraft_version,
            "loader_version"   : project.loader_version
        ]
        inputs.properties(map)

        filesMatching("fabric.mod.json") {
            expand(map)
        }
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}